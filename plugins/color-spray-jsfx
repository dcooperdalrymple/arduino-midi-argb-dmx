desc:Color Spray

slider1:channel=8<0,15,1{1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16}>MIDI Channel
slider2:mode=1<0,5,1{Off,RGB,HSV,Palette,ChaseRgb,ChaseHsv}>Color Mode

slider3:palette=0<0,7,1{Cloud,Lava,Ocean,Forest,Rainbow,Rainbow Stripe,Party,Heat}>Palette
slider4:speed=1<0,3.96875,0.03125>Speed

slider5:r=0<0,127,1>R
slider6:g=0<0,127,1>G
slider7:b=0<0,127,1>B

slider8:h=0<0,127,1>H
slider9:s=0<0,127,1>S
slider10:v=0<0,127,1>V

slider11:count=1<1,8,1>Count

@init
pcbyte = $xc0;
ccbyte = $xb0;

curChannel = 0;
curMode = -1;
curPalette = -1;
curSpeed = -1;
curR = -1;
curG = -1;
curB = -1;
curH = -1;
curS = -1;
curV = -1;
curCount = -1;

prevMode = -1;
prevPalette = -1;
prevSpeed = -1;
prevR = -1;
prevG = -1;
prevB = -1;
prevH = -1;
prevS = -1;
prevV = -1;
prevCount = -1;

function maybeSendPC(chan, cur, prev)
(
  cur != prev ? midisend(0, pcbyte | (chan & $x0f), cur & $x7f);
  cur;
);

function maybeSendCC(chan, ctrl, cur, prev)
(
  cur != prev ? midisend(0, ccbyte | (chan & $x0f), ctrl & $x7f, cur & $x7f);
  cur;
);

@slider
curChannel = channel;
curMode = mode > 0 ? mode + 15 : mode;
curPalette = palette;
curSpeed = speed;
curR = r;
curG = g;
curB = b;
curH = h;
curS = s;
curV = v;
curCount = count;

@block
prevMode = maybeSendPC(curChannel, curMode, prevMode);

prevPalette = maybeSendCC(curChannel, 12, curPalette * 16, prevPalette);
prevSpeed = maybeSendCC(curChannel, 13, curSpeed * 32, prevSpeed);

prevR = maybeSendCC(curChannel, 16, curR, prevR);
prevG = maybeSendCC(curChannel, 17, curG, prevG);
prevB = maybeSendCC(curChannel, 18, curB, prevB);

prevH = maybeSendCC(curChannel, 80, curH, prevH);
prevS = maybeSendCC(curChannel, 81, curS, prevS);
prevV = maybeSendCC(curChannel, 82, curV, prevV);

prevCount = maybeSendCC(curChannel, 83, (curCount - 1) * 127 / 7, prevCount);
